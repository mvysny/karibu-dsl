buildscript {
    ext.kotlin_version = '1.2.10'
    ext.vaadin8_version = '8.2.0.rc1'
      // when upgrading Vaadin 10 version, make sure to update Bower dependencies, by reverse-engineering that webjar crap
    // at https://github.com/vaadin/beverage-starter-flow/blob/master/pom.xml
    // most easily by doing mvn package and then inspecting webjar versions in the WAR file
    ext.vaadin10_version = '10.0.0.alpha8'
    ext.flow_version = '1.0.0.alpha12'
    ext.local = new Properties()
    if (project.rootProject.file('local.properties').exists()) {
        ext.local.load(project.rootProject.file('local.properties').newDataInputStream())
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.10"
    id "org.akhikhl.gretty" version "2.0.0"
}

repositories {
    mavenCentral()
}

defaultTasks 'clean', 'build'

allprojects {
    group 'com.github.vok.karibudsl'
    version '0.2.15'
}

subprojects {

    apply plugin: 'kotlin'

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'http://maven.vaadin.com/vaadin-addons' }
        maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
    }

    // for Travis to record failures: http://stackoverflow.com/questions/28614865/how-to-read-test-result-reports-on-travis-ci/28617954#28617954
    test {
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }
}
