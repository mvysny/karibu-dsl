plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

dependencies {
    compile(project(':karibu-dsl-v8'))
    compile "com.vaadin:vaadin-compatibility-server:$vaadin8_version"
}

task sourceJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

apply plugin: 'maven-publish'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId 'karibu-dsl-v8compat7'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Karibu-DSL, Kotlin extensions/DSL for Vaadin')
                root.appendNode('name', 'Karibu-DSL v8 compatibility layer for v7')
                root.appendNode('url', 'https://github.com/mvysny/karibu-dsl')
            }
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

build.dependsOn publishToMavenLocal

bintray {
    user = local.getProperty('bintray.user')
    key = local.getProperty('bintray.key')
    pkg {
        repo = 'vaadin-on-kotlin'
        name = 'vaadin-on-kotlin'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/mvysny/karibu-dsl'
        publish = true
        publications = ['mavenJava']
        version {
            name = project.version
            released = new Date()
        }
    }
}
